The first step is to create a so called RWT _entry point_ class. An instance of this class needs to
be registered as a Spring bean to be picked up by the RWT configuration. The
suggested approach is to use a Grails [service|http://grails.org/doc/latest/guide/services.html]
for that. Being a Grails artifact, a service is registered as a Spring bean by default
and other dependencies and functionalities are added/injected out of the box.

The following code snippet shows an example entry point service implementation
(in the @grails-app/services@ sub-folder). To demonstrate the usage,
another service (@FooService@) and a domain class (@Foo@) are used. They don't do anything useful and
their code can be obtained from the example application, as described above. The comments in the code
snippet highlight interesting points.

{code}
package grails.rwt.example

import org.eclipse.jface.layout.GridDataFactory
import org.eclipse.jface.layout.GridLayoutFactory
import org.eclipse.jface.viewers.ArrayContentProvider
import org.eclipse.jface.viewers.LabelProvider
import org.eclipse.jface.viewers.ListViewer
import org.eclipse.rap.rwt.application.AbstractEntryPoint
import org.eclipse.swt.SWT
import org.eclipse.swt.events.SelectionAdapter
import org.eclipse.swt.events.SelectionEvent
import org.eclipse.swt.widgets.Button
import org.eclipse.swt.widgets.Composite
import org.eclipse.swt.widgets.Display
import org.eclipse.swt.widgets.Label
import org.eclipse.swt.widgets.Shell

// "Note the use of the Groovy syntax"

class HelloEntryPointService extends AbstractEntryPoint { // "The service needs to implement EntryPoint"

    def fooService // "Other services will get injected by Grails"

    @Override
    protected void createContents(Composite parent) {
        log.info("Creating RWT UI ...") // "Implicit logger object"

        final Label label = new Label(parent, SWT.NONE)
        GridDataFactory.swtDefaults().align(SWT.FILL, SWT.CENTER).grab(true, false).applyTo(label)

        // "JFace is available"
        ListViewer viewer = new ListViewer(parent, SWT.V_SCROLL | SWT.H_SCROLL)
        GridDataFactory.swtDefaults().hint(500, 300).applyTo(viewer.list)
        viewer.contentProvider = ArrayContentProvider.instance
        viewer.labelProvider = new LabelProvider()
        viewer.input = Foo.list() // "Use domain objects and GORM, no DTOs required!"

        Button button = new Button(parent, SWT.PUSH)
        GridDataFactory.swtDefaults().align(SWT.BEGINNING, SWT.CENTER).applyTo(button)
        button.text = "Click me!"
        button.addSelectionListener(new SelectionAdapter() {
            Integer num = 0

            @Override
            public void widgetSelected(SelectionEvent e) {
                label.text = fooService.serviceMethod(num++) // "Call methods of other services"
            }
        })
    }

    @Override
    protected Shell createShell(Display display) {
        Shell shell = new Shell(display)
        shell.setText("Hello RWT!")
        GridLayoutFactory.swtDefaults().applyTo(shell)
        return shell
    }
}
{code}

According to the Grails conventions, a Spring bean called @helloEntryPointService@ will be registered
automatically on application start. The next step is to configure our newly created entry point.

{note}
The given code snippet is just a very simple example to demonstrate the usage. You should develop
your application using a cleaner separation of concerns, i.e. by employing the MVC or MVP pattern.
{note}